plugins {
    id 'java'
}

sourceSets {
    main {
        java {
            srcDir '.'
            include 'BowtieJsonSchemaValidator.java'
        }
    }
}

repositories {
    mavenCentral()
}


def harnessImplementation = [
    group: 'com.github.java-json-tools',
    name: 'json-schema-validator',
    version: '2.2.14'
]

dependencies {
    implementation "$harnessImplementation.group:$harnessImplementation.name:$harnessImplementation.version"

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.19.0'
}

jar {
    archiveBaseName = 'harness'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
        attributes "Main-Class": "BowtieJsonSchemaValidator"
        attributes "Implementation-Name": "$harnessImplementation.group-$harnessImplementation.name"
        attributes "Implementation-Version": harnessImplementation.version
    }
}
